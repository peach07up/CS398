# spimbot constants
ANGLE = 0xffff0014
ANGLE_CONTROL = 0xffff0018
TIMER = 0xffff001c
HEAD_X = 0xffff0020
HEAD_Y = 0xffff0024
BONK_ACKNOWLEDGE = 0xffff0060
TIMER_ACKNOWLEDGE = 0xffff006c
APPLE_X = 0xffff0070
APPLE_Y = 0xffff0074

.kdata                # interrupt handler data (separated just for readability)
chunkIH:.space 32      # space for eight registers
non_intrpt_str:   .asciiz "Non-interrupt exception\n"
unhandled_str:    .asciiz "Unhandled interrupt type\n"


.ktext 0x80000180
interrupt_handler:
.set noat
      move      $k1, $at               # Save $at                               
.set at
      la      $k0, chunkIH                
      sw      $a0, 0($k0)              # Get some free registers                  
      sw      $a1, 4($k0)              # by storing them to a global variable     
      sw      $a2, 8($k0)              
      sw      $a3, 12($k0)               
      sw      $v0, 16($k0)               
      sw      $v1, 20($k0)               

      mfc0    $k0, $13                 # Get Cause register                       
      srl     $a0, $k0, 2                
      and     $a0, $a0, 0xf            # ExcCode field                            
      bne     $a0, 0, non_intrpt         

interrupt_dispatch:                    # Interrupt:                             
      mfc0    $k0, $13                 # Get Cause register, again                 
      beq     $k0, $zero, done         # handled all outstanding interrupts     
  
      and     $a0, $k0, 0x1000         # is there a bonk interrupt?                
      bne     $a0, 0, bonk_interrupt   

      and     $a0, $k0, 0x8000         # is there a timer interrupt?
      bne     $a0, 0, timer_interrupt

                         # add dispatch for other interrupt types here.

      li      $v0, 4                   # Unhandled interrupt types

      la      $a0, unhandled_str
      syscall 
      j       done

bonk_interrupt:
      sw      $zero, 0xffff0010($zero) # ???
      sw      $a1, BONK_ACKNOWLEDGE($zero)   # acknowledge interrupt

      j       interrupt_dispatch       # see if other interrupts are waiting

timer_interrupt:
      sw      $a1, 0xffff006c($zero)   # acknowledge interrupt

      lw 	$a0, HEAD_X
      lw 	$a1, HEAD_Y
      lw 	$a2, APPLE_X
      lw 	$a3, APPLE_Y

	beq  	$a0, $a2, na_do_y
	bgt	$a0, $a2, na_180
	li	$v0, 0
	j	ti_cont
na_180:	li	$v0, 180
	j	ti_cont
na_do_y:bge	$a1, $a3, na_270
	li	$v0, 90
	j	ti_cont
na_270:	li	$v0, 270

ti_cont:
	lw	$v1, prev_angle($0)
      sub	$v1, $v1, $v0	     # prev_angle - angle
      abs	$v1, $v1	     # if the abs of angle diffs is 180
      bne	$v1, 180, set_direction	# then this would be a U-turn
      add	$v1, $v0, 90         # can't do U-turns
      sw 	$v1, ANGLE      # set angle to + 90
      li	$v1, 1
      sw 	$v1, ANGLE_CONTROL

	li	$v1, 2500	     # and wait ~5000 cycles
wait:	add	$v1, $v1, -1
	bne	$v1, $0, wait

set_direction:	
	sw 	$v0, ANGLE
	li	$v1, 1
	sw 	$v1, ANGLE_CONTROL
	sw	$v0, prev_angle($0)    # remember previous direction

      lw      $v0, 0xffff001c($0)      # current time
      add     $v0, $v0, 500
      sw      $v0, 0xffff001c($0)      # request timer in 500

      j       interrupt_dispatch       # see if other interrupts are waiting

non_intrpt:                            # was some non-interrupt
      li      $v0, 4
      la      $a0, non_intrpt_str
      syscall                          # print out an error message

      # fall through to done

done:
      la      $k0, chunkIH
      lw      $a0, 0($k0)              # Restore saved registers
      lw      $a1, 4($k0)
      lw      $a2, 8($k0)              
      lw      $a3, 12($k0)               
      lw      $v0, 16($k0)               
      lw      $v1, 20($k0)               
.set noat
      move    $at, $k1                 # Restore $at
.set at 
      eret
